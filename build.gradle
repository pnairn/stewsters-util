// http://yennicktrevels.com/blog/2013/10/11/automated-gradle-project-deployment-to-sonatype-oss-repository/
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.stewsters.util'
version = '0.7'


sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.11'
}

////TODO: we might not need this now
//jar.doFirst {
//    sourceSets.main.java.srcDirs.each {
//        from it
//    }
//}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Stewsters Util'
                packaging 'jar'
                description 'Some utilities for roguelike games.'
                url 'https://github.com/stewsters/stewsters-util'

                scm {
                    url 'scm:git@github.com:stewsters/stewsters-util.git'
                    connection 'scm:git@github.com:stewsters/stewsters-util.git'
                    developerConnection 'scm:git@github.com:stewsters/stewsters-util.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'stewsters'
                        name 'Adrian Moore'
                    }
                }
            }
        }
    }
}