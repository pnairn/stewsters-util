buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath "net.saliman:gradle-cobertura-plugin:2.2.5"
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.3.1"
    }
}
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:all"
        }
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'net.saliman.cobertura'
apply plugin: "me.champeau.gradle.jmh"

sourceCompatibility = 1.7
targetCompatibility = 1.7


group = 'com.stewsters.util'
version = '0.17'


if (!project.hasProperty('bintray_net_user')) {
    project.ext.bintray_net_user = 'default'
}
if (!project.hasProperty('bintray_net_key')) {
    project.ext.bintray_net_key = 'fake'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile "org.codehaus.groovy:groovy-all:2.4.7"
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}


bintray {
    apiUrl = "https://api.bintray.com"

    user = bintray_net_user
    key = bintray_net_key

    publications = ['mavenJava']

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'maven'
        name = 'stewsters-util'
        desc = 'A package of Java roguelike utilities.'
        websiteUrl = 'https://github.com/stewsters/stewsters-util'
        issueTrackerUrl = 'https://github.com/stewsters/stewsters-util/issues'
        vcsUrl = 'https://github.com/stewsters/stewsters-util.git'
        licenses = ['MIT']
        labels = ['roguelike', 'utilities']

        publicDownloadNumbers = true

        //Optional version descriptor
        version {
            name = "0.17" //Bintray logical version name
            vcsTag = "0.17"
        }
    }

}

jmh {
   // include = ['PathfindingBenchmark']

    iterations = 5
    fork = 5
    warmupIterations = 5

    jmhVersion = '1.12'
}